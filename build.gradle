plugins {
    id 'net.researchgate.release' version '2.7.0'
}

group 'com.vevo.upsilon'

//dummy build task for root due to https://github.com/researchgate/gradle-release/issues/186
task build{}

//for each submodule that we want to actually release, add to these depends on
afterReleaseBuild.dependsOn ':core:publish'
afterReleaseBuild.dependsOn ':ext:s3:publish'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'maven'

    sourceCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "http://nexus.vevodev.com/content/repositories/releases/"
        }
        jcenter()
    }

    build {
        dependsOn publishToMavenLocal
    }

    test {
        useTestNG()
    }

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.22'

        testCompile 'org.testng:testng:6.10'
        testCompile 'org.mockito:mockito-core:2.5.4'
        testCompile 'org.slf4j:slf4j-simple:1.7.22'
    }

    compileJava {
        options.compilerArgs += ["-parameters"]
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }

    // All of this can maybe go away if https://issues.gradle.org/browse/GRADLE-2365 gets solved
    def getCreds = { id ->
        def username = System.getenv("NEXUS_USER")
        def password = System.getenv("NEXUS_PASSWORD")

        if (!(username && password && username.trim() && password.trim())) {
            File mavenSettings = new File("${System.properties['user.home']}/.m2/settings.xml")
            if (mavenSettings.exists()) {
                def xmlSlurper = new XmlSlurper()
                def settings = xmlSlurper.parse(mavenSettings)
                username = settings.servers.children().find { it.id == id }.username
                password = settings.servers.children().find { it.id == id }.password
            }
        }

        [username: username, password: password]
    }

    def releaseCreds = getCreds('releases')

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifact sourceJar
            }
        }

        repositories {
            maven {
                url "http://nexus.vevodev.com/content/repositories/releases/"
                credentials {
                    username = releaseCreds.username
                    password = releaseCreds.password
                }
            }
        }
    }
}
