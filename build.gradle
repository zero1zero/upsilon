plugins {
    id 'net.researchgate.release' version '2.7.0'
    id 'net.linguica.maven-settings' version '0.5'
}

group 'com.vevo.upsilon'

//dummy build task for root due to https://github.com/researchgate/gradle-release/issues/186
task build{}

//for each submodule that we want to actually release, add to these depends on
afterReleaseBuild.dependsOn ':core:publish'
afterReleaseBuild.dependsOn ':ext:s3:publish'
afterReleaseBuild.dependsOn ':ext:jdbc:publish'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    sourceCompatibility = 11

    repositories {
        mavenLocal()
        mavenCentral()
    }

    build {
        dependsOn publishToMavenLocal
    }

    test {
        useTestNG()
    }

    dependencies {
        implementation 'org.slf4j:slf4j-api:1.7.22'

        testImplementation 'org.testng:testng:6.10'
        testImplementation  'org.mockito:mockito-core:2.5.4'
        testImplementation 'org.slf4j:slf4j-simple:1.7.22'
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << '-parameters'
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifact sourceJar
            }
        }

        repositories {
            maven {
                name 'vevo'
                url "http://nexus.vevodev.com/content/repositories/releases/"
            }
        }
    }
}
